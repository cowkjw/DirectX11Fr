#include "Engine_Shader_Defines.hlsli"

/* 상수테이블 ConstantTable */
matrix g_WorldMatrix, g_ViewMatrix, g_ProjMatrix;
texture2D g_Texture;
texture2D g_DepthTexture;
float4 g_vColor;

float g_fCameraFar;


/* 정점의 기초적인 변환 (월드변환, 뷰, 투영변환) */
/* 정점의 구성 정보를 변형할 수 있다. */

struct VS_IN
{
    float3 vPosition : POSITION;
    float2 vTexcoord : TEXCOORD0;
};

struct VS_OUT
{
    /* SV_ : ShaderValue약자 */
    /* 내가 해야할 연산은 다 했으니 이제 니(장치)가 알아서 추가적인 연산을 해라. */
    float4 vPosition : SV_POSITION;
    float2 vTexcoord : TEXCOORD0;
};

VS_OUT VS_MAIN(VS_IN In)
{
    VS_OUT Out;

    matrix matWV, matWVP;

    /* mul : 모든 행렬의 곱하기를 수행한다. /w연산을 수행하지 않는다. */
    matWV = mul(g_WorldMatrix, g_ViewMatrix);
    matWVP = mul(matWV, g_ProjMatrix);

    Out.vPosition = mul(vector(In.vPosition, 1.f), matWVP);
    Out.vTexcoord = In.vTexcoord;

    return Out;
}

struct VS_OUT_BLEND
{
    float4 vPosition : SV_POSITION;
    float2 vTexcoord : TEXCOORD0;
    float4 vProjPos : TEXCOORD1;

};

VS_OUT_BLEND VS_MAIN_BLEND(VS_IN In)
{
    VS_OUT_BLEND Out;

    matrix matWV, matWVP;

    /* mul : 모든 행렬의 곱하기를 수행한다. /w연산을 수행하지 않는다. */
    matWV = mul(g_WorldMatrix, g_ViewMatrix);
    matWVP = mul(matWV, g_ProjMatrix);

    Out.vPosition = mul(vector(In.vPosition, 1.f), matWVP);
    Out.vTexcoord = In.vTexcoord;
    Out.vProjPos = Out.vPosition;

    return Out;
}

//POSITION시멘틱이 붙은
//멤버변수에 대해서

/* W나누기 연산을 수행한다. */
/* 뷰포트로 변환한다. */
/* 모든 성분에 대해서 래스터라이즈. -> 픽셀을 생성한다. */


struct PS_IN
{
    float4 vPosition : SV_POSITION;
    float2 vTexcoord : TEXCOORD0;

};

struct PS_OUT
{
    vector vColor : SV_TARGET0;
};

PS_OUT PS_MAIN(PS_IN In)
{
    PS_OUT Out;

    Out.vColor = g_Texture.Sample(DefaultSampler, In.vTexcoord);

    return Out;
}

PS_OUT PS_MAIN_Radial(PS_IN In)
{
	PS_OUT Out;

    float2 uv = In.vTexcoord;

    float mask = g_Texture.Sample(DefaultSampler, uv).r;

    if (mask < 0.1f)
        discard;
    Out.vColor = g_vColor;
    Out.vColor.a = mask * g_vColor.a;
    return Out;
}


struct PS_IN_BLEND
{
    float4 vPosition : SV_POSITION;
    float2 vTexcoord : TEXCOORD0;
    float4 vProjPos : TEXCOORD1;
};


PS_OUT PS_MAIN_BLEND(PS_IN_BLEND In)
{
    PS_OUT Out;

    Out.vColor = g_Texture.Sample(DefaultSampler, In.vTexcoord);


    /*화면 전체 기준(0, 0 ~ 1, 1)으로 이펙트의 픽셀이 그려질 위치에 해당하는 좌표 */
    float2 vTexcoord;

    /*이펙트의 특정 픽셀(psin)이 화면 전체기준으로 어디에 존재하는지? */
    /* 우선 투영공간상(-1, 1 -> 1, -1)의 픽셀의 위치를 구한다.*/
    vTexcoord.x = In.vProjPos.x / In.vProjPos.w;
    vTexcoord.y = In.vProjPos.y / In.vProjPos.w;

    vTexcoord.x = vTexcoord.x * 0.5f + 0.5f;
    vTexcoord.y = vTexcoord.y * -0.5f + 0.5f;

    vector vDepthDesc = g_DepthTexture.Sample(DefaultSampler, vTexcoord);

    float fOldViewZ = vDepthDesc.y * g_fCameraFar;

    Out.vColor.a = Out.vColor.a * saturate(fOldViewZ - In.vProjPos.w);

    return Out;
}

PS_OUT PS_MAIN_SoftRadial(PS_IN_BLEND In)
{
    PS_OUT Out;

    float2 uv = In.vTexcoord;

    float mask = g_Texture.Sample(DefaultSampler, uv).r;
    if (mask <= 0.1f) discard;

    float2 screenUV;
    screenUV.x = In.vProjPos.x / In.vProjPos.w;
    screenUV.y = In.vProjPos.y / In.vProjPos.w;
    screenUV = screenUV * float2(0.5f, -0.5f) + float2(0.5f, 0.5f);

    float sceneDepth = g_DepthTexture.Sample(DefaultSampler, screenUV).y * g_fCameraFar;
    float depthFade = saturate(sceneDepth - In.vProjPos.w);

    Out.vColor.rgb = g_vColor.rgb;
    Out.vColor.a = mask * depthFade * g_vColor.a;

    return Out;
}


technique11 DefaultTechnique
{
    /* 패스를 생성하는 기준을 뭘로? */
    /* 같은 모델을 그릴때 각기 다른 렌더스테이츠를 먹여야하거나.
    완전히 다른 쉐이딩 기법을 적용해야하거나 */

    /* 각 쉐이더를 어떤 버젼으로 빌드할건지.  */
    /* 어떤 쉐이더를 사용할건지? */
    /* 진입점 함수 결정 */
    /* 렌더스테이츠에 대한 설정*/
    pass Default/* 명암 + 스펙큘러 + 그림자 + ssao + 림라이트 */
    {
       SetRasterizerState(RS_Default);
        SetDepthStencilState(DSS_None, 0);
        SetBlendState(BS_AlphaBlend, float4(0.f, 0.f, 0.f, 0.f), 0xffffffff);


        VertexShader = compile vs_5_0 VS_MAIN();
        PixelShader = compile ps_5_0 PS_MAIN();
    }
    pass Radial
    {  
        SetRasterizerState(RS_Default);
        SetDepthStencilState(DSS_Default, 0);
        SetBlendState(BS_AlphaBlend, float4(0.f, 0.f, 0.f, 0.f), 0xffffffff);
        VertexShader = compile vs_5_0 VS_MAIN();
        PixelShader = compile ps_5_0 PS_MAIN_Radial();
    }

     pass SoftEffect
    {
        SetRasterizerState(RS_Default);
        SetDepthStencilState(DSS_Default, 0);
        SetBlendState(BS_AlphaBlend, float4(0.f, 0.f, 0.f, 0.f), 0xffffffff);

        VertexShader = compile vs_5_0 VS_MAIN_BLEND();
        PixelShader = compile ps_5_0 PS_MAIN_BLEND();
    }
}
